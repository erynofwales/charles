# SConscript
# vim: set ft=python:
# Eryn Wells <eryn@erynwells.me>

Import('env')


objs = []

subdirs = [
    'yaml',
]

for d in subdirs:
    dir_objs = env.SConscript(env.Dir(d).File('SConscript'), {'env': env})
    objs.extend(dir_objs)


files = [
    'basics.cc',
    'camera.cc',
    'light.cc',
    'material.cc',
    'object.cc',
    'object_sphere.cc',
    'object_plane.cc',
    'reader_yaml.cc',
    'scene.cc',
    'writer_png.cc',
]

for f in files:
    objs.append(env.Object(f))

lib = env.Library('charles', objs)
prog = env.Program('charles', ['charles.cc'], LIBS=[lib, 'png', 'yaml'])
env.Alias('charles', prog)

env.Default('charles')
